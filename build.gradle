apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'application'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

project.ext {
    dropwizardVersion = '1.2.0'
    jUnitVersion = '4.12'
    mockitoVersion = '2.12.0'
}

dependencies {
    compile(
            "io.dropwizard:dropwizard-core:$dropwizardVersion",
            "com.github.arteam:dropwizard-jdbi3:1.2.1",
            "org.jdbi:jdbi3-sqlobject:3.0.0-rc2",
            "org.xerial:sqlite-jdbc:3.21.0",
            "org.postgresql:postgresql:42.1.4",
            "org.flywaydb:flyway-core:4.2.0"
    )

    testCompile(
            "io.dropwizard:dropwizard-testing:$dropwizardVersion",
            "org.mockito:mockito-core:$mockitoVersion",
            "junit:junit:$jUnitVersion",
            'io.dropwizard:dropwizard-client:1.2.1',
            "com.h2database:h2:1.4.196"
    )
}

sourceSets {
    integrationTest {
        java {
            srcDir 'src/integration-test/java'
        }
        resources {
            srcDir 'src/integration-test/resources'
        }
        compileClasspath += sourceSets.main.runtimeClasspath
        compileClasspath += sourceSets.test.runtimeClasspath
        compileClasspath += sourceSets.test.output
    }
}

distributions {
    main {
        contents {
            from {
                ['README.md', 'verify-local-matching-service-example.yml']
            }
        }
    }
}

run {
    args = ["server", "verify-local-matching-service-example.yml"]
}

mainClassName = 'uk.gov.ida.verifylocalmatchingserviceexample.VerifyLocalMatchingServiceExampleApplication'