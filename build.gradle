apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'application'

sourceCompatibility = 1.8


repositories {
    if (System.getenv('VERIFY_USE_PUBLIC_BINARIES') == 'true') {
        logger.warn('Production builds MUST NOT be built with public binaries.\nUse artifactory/whitelisted-repos for production builds.\n\n')
        maven { url 'https://dl.bintray.com/alphagov/maven-test' }
        maven { url 'https://build.shibboleth.net/nexus/content/repositories/releases' }
        jcenter()
    }
    else {
        maven { url 'https://artifactory.ida.digital.cabinet-office.gov.uk/artifactory/whitelisted-repos' }
    }
}

project.ext {
    dropwizardVersion = '1.2.0'
    jUnitVersion = '4.12'
    mockitoVersion = '2.12.0'
}

dependencies {
    compile(
            "io.dropwizard:dropwizard-core:$dropwizardVersion",
            "org.jdbi:jdbi3-core:3.0.0",
            "org.postgresql:postgresql:42.1.4",
            "org.flywaydb:flyway-core:4.2.0"
    )

    testCompile(
            "io.dropwizard:dropwizard-testing:$dropwizardVersion",
            "org.mockito:mockito-core:$mockitoVersion",
            "junit:junit:$jUnitVersion",
            'io.dropwizard:dropwizard-client:1.2.1',
            "com.h2database:h2:1.4.196"
    )
}

sourceSets {
    integrationTest {
        java {
            srcDir 'src/integration-test/java'
        }
        resources {
            srcDir 'src/integration-test/resources'
        }
        compileClasspath += sourceSets.main.runtimeClasspath
        compileClasspath += sourceSets.test.runtimeClasspath
        compileClasspath += sourceSets.test.output
    }
}

task integrationTest(type: Test) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath += sourceSets.integrationTest.runtimeClasspath
}

distributions {
    main {
        contents {
            from {
                ['README.md', 'verify-local-matching-service-example.yml']
            }
        }
    }
}

mainClassName = 'uk.gov.ida.verifylocalmatchingserviceexample.VerifyLocalMatchingServiceExampleApplication'
